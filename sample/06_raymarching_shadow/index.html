<!DOCTYPE html><script id="f" type="x/f">precision mediump float;uniform float t;uniform vec2 r;
const vec3 cPos = vec3(0.0, 5.0, 5.0);
const vec3 cDir = vec3(0.0, -0.707, -0.707);
const vec3 cUp  = vec3(0.0,  0.707, -0.707);
const vec3 lightDir = vec3(0.0, 1.0, 0.0);
float distFuncFloor(vec3 p){
	return dot(p, vec3(0.0, 1.0, 0.0)) + 2.0;
}
float distFuncTorus(vec3 p){
	const vec2 t = vec2(2.5, 0.75);
	vec2 r = vec2(length(p.xz) - t.x, p.y);
	return length(r) - t.y;
}
float distFunc(vec3 p){
	float d1 = distFuncTorus(p);float d2 = distFuncFloor(p);return min(d1, d2);
}
vec3 genNormal(vec3 p){
	float d = 0.0001;
	return normalize(vec3(
		distFunc(p + vec3(  d, 0.0, 0.0)) - distFunc(p + vec3( -d, 0.0, 0.0)),
		distFunc(p + vec3(0.0,   d, 0.0)) - distFunc(p + vec3(0.0,  -d, 0.0)),
		distFunc(p + vec3(0.0, 0.0,   d)) - distFunc(p + vec3(0.0, 0.0,  -d))
	));
}
float genShadow(vec3 ro, vec3 rd){
	float h = 0.0;
	float c = 0.0;
	float r = 1.0;
	float shadowCoef = 0.5;
	for(float t = 0.0; t < 50.0; t++){
		h = distFunc(ro + rd * c);
		if(h < 0.001){
			return shadowCoef;
			break;
		}
		r = min(r, h * 16.0 / c);
		c += h;
	}
	return 1.0 - shadowCoef + r * shadowCoef;
}
void main(void){
	vec2 p = (gl_FragCoord.xy * 2.0 - r) / max(r.x, r.y);
	vec3 cSide = cross(cDir, cUp);
	float targetDepth = 1.0;
	vec3 ray = normalize(cSide * p.x + cUp * p.y + cDir * targetDepth);
	vec3 light = normalize(lightDir - vec3(sin(t), 0.0, 0.0));
	float tmp, dist;
	int j;
	tmp = 0.0;
	vec3  dPos = cPos;
	for(int i = 0; i < 128; i++){
		j = i;
		dist = distFunc(dPos);
		if(abs(dist) < 0.001){break;}
		tmp += dist;
		dPos = cPos + tmp * ray * 0.9;
	}
	vec3 color;
	float shadow = 1.0;
	if(abs(dist) < 0.001){
		vec3 normal = genNormal(dPos);
		vec3 halfLE = normalize(light - ray);
		float diff = clamp(dot(light, normal), 0.1, 1.0) * 0.75;
		float spec = pow(clamp(dot(halfLE, normal), 0.0, 1.0), 50.0);
		shadow = genShadow(dPos + normal * 0.001, light);
		float u = 1.0 - floor(mod(dPos.x, 2.0)) * 0.5;
		float v = 1.0 - floor(mod(dPos.z, 2.0)) * 0.5;
		if((u == 1.0 && v < 1.0) || (u < 1.0 && v == 1.0)){diff *= 0.7;}
		color = diff + vec3(spec);
	}else{
		color = vec3(0.0);
	}
	gl_FragColor = vec4(color * shadow, 1.0);
}
</script><script id="v" type="x/v">attribute vec3 p;void main(){gl_Position=vec4(p,1.);}</script><script>w=window;w.onload=function(){b=function(s){return document.getElementById(s)};w.addEventListener('keydown',k,1);c=b('c');c.width=x=w.innerWidth;c.height=y=w.innerHeight;g=c.getContext('webgl');p=g.createProgram();h=function(i,j){k=g.createShader(35633-i);g.shaderSource(k,j);g.compileShader(k);g.attachShader(p,k);return g.getShaderInfoLog(k);};if(!h(0,b('v').text) && !h(1,b('f').text)){g.linkProgram(p);}e=g.getProgramParameter(p, 35714);g.useProgram(p);u={};u.t=g.getUniformLocation(p,'t');u.r=g.getUniformLocation(p,'r');g.bindBuffer(34962,g.createBuffer());g.bufferData(34962,new Float32Array([-1,1,0,-1,-1,0,1,1,0,1,-1,0]),35044);a=g.getAttribLocation(p,'p');g.enableVertexAttribArray(a);g.vertexAttribPointer(a,3,5126,0,0,0);g.clearColor(0,0,0,1);z=Date.now();(function(){if(!e){return;}c.width=x=w.innerWidth;c.height=y=w.innerHeight;g.viewport(0,0,x,y);d=(Date.now()-z)*.001;g.clear(16384);g.uniform1f(u.t,d);g.uniform2fv(u.r,[x,y]);g.drawArrays(5,0,4);g.flush();setTimeout(arguments.callee,16);})();function k(h){e=(h.keyCode!==27);}};</script><style>*{margin:0;padding:0;overflow:hidden}html,body{height:100%}</style><canvas id="c"></canvas>
