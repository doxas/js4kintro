<!DOCTYPE html><script id="f" type="x/f">precision mediump float;uniform float t;uniform vec2 r;
const vec3 cPos = vec3(0.0, 0.0, 5.0);
const vec3 cDir = vec3(0.0, 0.0, -1.0);
const vec3 cUp  = vec3(0.0, 1.0, 0.0);
const vec3 lightDir = vec3(0.577, 0.577, 0.577);
vec3 twist(vec3 p, float power){
	float s = sin(power * p.y);
	float c = cos(power * p.y);
	mat3 m = mat3(
		  c, 0.0,  -s,
		0.0, 1.0, 0.0,
		  s, 0.0,   c
	);
	return m * p;
}
float smoothMin(float d1, float d2, float k){
	float h = exp(-k * d1) + exp(-k * d2);
	return -log(h) / k;
}
float distFuncTorus(vec3 p, vec2 r){
	vec2 d = vec2(length(p.xy) - r.x, p.z);
	return length(d) - r.y;
}
float distFuncBox(vec3 p){
	return length(max(abs(p) - vec3(2.0, 0.1, 0.5), 0.0)) - 0.1;
}
float distFuncCylinder(vec3 p, vec2 r){
	vec2 d = abs(vec2(length(p.xy), p.z)) - r;
	return min(max(d.x, d.y), 0.0) + length(max(d, 0.0)) - 0.1;
}
float distFunc(vec3 p){
	vec3 q = twist(p, sin(t * 0.2) * 2.0);
	float d1 = distFuncTorus(q, vec2(1.5, 0.25));
	float d2 = distFuncBox(q);
	float d3 = distFuncCylinder(q, vec2(0.75, 0.25));
	return smoothMin(smoothMin(d1, d2, 16.0), d3, 16.0);
}
vec3 genNormal(vec3 p){
	float d = 0.001;
	return normalize(vec3(
		distFunc(p + vec3(  d, 0.0, 0.0)) - distFunc(p + vec3( -d, 0.0, 0.0)),
		distFunc(p + vec3(0.0,   d, 0.0)) - distFunc(p + vec3(0.0,  -d, 0.0)),
		distFunc(p + vec3(0.0, 0.0,   d)) - distFunc(p + vec3(0.0, 0.0,  -d))
	));
}
void main(void){
	vec2 p = (gl_FragCoord.xy * 2.0 - r) / max(r.x, r.y);
	vec3 cSide = cross(cDir, cUp);
	float targetDepth = 1.0;
	vec3 ray = normalize(cSide * p.x + cUp * p.y + cDir * targetDepth);
	float tmp, dist;
	tmp = 0.0;
	vec3 dPos = cPos;
	for(int i = 0; i < 256; i++){
		dist = distFunc(dPos);
		tmp += dist;
		dPos = cPos + tmp * ray * 0.75;
	}
	vec3 color;
	if(abs(dist) < 0.001){
		vec3 normal = genNormal(dPos);
		float diff = clamp(dot(lightDir, normal), 0.1, 1.0);
		color = vec3(1.0, 1.0, 1.0) * diff;
	}else{
		color = vec3(0.0);
	}
	gl_FragColor = vec4(color, 1.0);
}
</script><script id="v" type="x/v">attribute vec3 p;void main(){gl_Position=vec4(p,1.);}</script><script>w=window;w.onload=function(){b=function(s){return document.getElementById(s)};w.addEventListener('keydown',k,1);c=b('c');c.width=x=w.innerWidth;c.height=y=w.innerHeight;g=c.getContext('webgl');p=g.createProgram();h=function(i,j){k=g.createShader(35633-i);g.shaderSource(k,j);g.compileShader(k);g.attachShader(p,k);return g.getShaderInfoLog(k);};if(!h(0,b('v').text) && !h(1,b('f').text)){g.linkProgram(p);}e=g.getProgramParameter(p, 35714);g.useProgram(p);u={};u.t=g.getUniformLocation(p,'t');u.r=g.getUniformLocation(p,'r');g.bindBuffer(34962,g.createBuffer());g.bufferData(34962,new Float32Array([-1,1,0,-1,-1,0,1,1,0,1,-1,0]),35044);a=g.getAttribLocation(p,'p');g.enableVertexAttribArray(a);g.vertexAttribPointer(a,3,5126,0,0,0);g.clearColor(0,0,0,1);z=Date.now();(function(){if(!e){return;}c.width=x=w.innerWidth;c.height=y=w.innerHeight;g.viewport(0,0,x,y);d=(Date.now()-z)*.001;g.clear(16384);g.uniform1f(u.t,d);g.uniform2fv(u.r,[x,y]);g.drawArrays(5,0,4);g.flush();setTimeout(arguments.callee,33);})();function k(h){e=(h.keyCode!==27);}};</script><style>*{margin:0;padding:0;overflow:hidden}html,body{height:100%}</style><canvas id="c"></canvas>
